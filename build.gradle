buildscript {
    repositories {
        maven {
            name = ":forge"
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "gradle.plugin.net.kyori:blossom:1.1.0"
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: "net.kyori.blossom"
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = modVersion
group = modGroup
archivesBaseName = modBaseName
//noinspection GroovyAssignabilityCheck
project.ic2_range= "[${project.ic2_min_ver},${project.ic2_max_ver}]"

blossom {
    replaceToken '%VERSION%', modVersion
    replaceToken "%IC2_RANGE%", ic2_range
    replaceToken "%CERTIFICATE%", fingerprint
}

minecraft {
    //noinspection GroovyAssignabilityCheck
    mappings channel: "snapshot", version: mcp_version
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    maven {
        name 'IC2'
        url 'http://maven.ic2.player.to'
        content {
            includeGroup 'net.industrial-craft'
        }
    }
    maven {
        name 'progwml6'
        url 'https://dvs1.progwml6.com/files/maven'
        content {
            includeGroup 'mezz.jei'
        }
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft([
            group  : "net.minecraftforge",
            name   : "forge",
            version: "${minecraft_version}-${forge_version}"
    ])

    compileOnly "curse.maven:thaumcraft-223628:2629023"
    compileOnly "curse.maven:wireless-solar-panels-377164:2954663"
    compileOnly "curse.maven:botania-225643:2846950"
    compileOnly "mezz.jei:jei_1.12.2:4.16.1.302:api"
    runtimeOnly "net.industrial-craft:industrialcraft-2:${project.ic2_max_ver}"
    compileOnly "net.industrial-craft:industrialcraft-2:2.8.193-ex112:dev"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft_version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version": project.version, "mcversion": project.minecraft_version,
                "ic2_range": project.ic2_range
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title": project.name,
                "Specification-Vendor": "ijo42",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"Denfop",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Git-Commit-Hash": getGitHash()
        ])
    }
}
apply from: 'https://gist.github.com/ijo42/6b215e76269a72f37194b924ddda038e/raw/d782226840813ae6703044c51e98b61eb4675875/signjar.groovy'
build.dependsOn signJar

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.incremental = true // one flag, and things will get so MUCH faster
    sourceCompatibility = targetCompatibility = '1.8'
}
jar.archiveFileName = "${project.archivesBaseName}-${project.minecraft_version}-${project.version}.jar"

jar.finalizedBy('reobfJar')
defaultTasks 'build'

static def getGitHash() {
    try {
        def process = 'git rev-parse --short HEAD'.execute()
        process.waitFor()
        return (process.exitValue() ? 'unknown' : process.text.trim())
    }catch(IOException ignored){
        return 'unknown'
    }
}